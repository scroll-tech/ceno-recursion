from "./poseidon.zok" import poseidon, ark, sbox, mix

const field A = 526
const field B = 265
const u32 WIDTH = 253

struct Point {
    field x
    field y
}

def point_add(Point a, Point b) -> Point:
    field m = a.x != b.x ? (b.y - a.y) / (b.x - a.x) : (3 * a.x * a.x + A) / (2 * a.y)
    field x3 = m * m - a.x - b.x
    field y3 = m * (a.x - x3) - a.y
    return Point {
        x: x3,
        y: y3
    }

// k_bits is in big-endian of length 253
// Use k_bits for multiplication while simultaneous check it matches k
def point_mul(Point a, field k, bool[0] k_bits) -> Point:
    field k_assemb = 1
    Point a_k = a
    u32 i = 0
    bool bit = k_bits[i]
    while !bit do
        i = i + 1
        bit = k_bits[i]
    endwhile
    i = i + 1
    while i < WIDTH do
        a_k = point_add(a_k, a_k)
        k_assemb = k_assemb * 2
        if k_bits[i] then
            a_k = point_add(a_k, a)
            k_assemb = k_assemb + 1 
        endif
        i = i + 1
    endwhile
    assert(k_assemb == k)
    return a_k

struct PublicKey {
    Point p
    Point q
}

struct Signature {
    Point r
    field s
}

def verify_signature(field msg, PublicKey pk, Signature sig, bool[0] e_bits, bool[0] s_bits) -> u32:
    // Check all points are on the curve
    assert(pk.p.y * pk.p.y == pk.p.x * pk.p.x * pk.p.x + A * pk.p.x + B)
    assert(pk.q.y * pk.q.y == pk.q.x * pk.q.x * pk.q.x + A * pk.q.x + B)
    assert(sig.r.y * sig.r.y == sig.r.x * sig.r.x * sig.r.x + A * sig.r.x + B)

    field[3] inputs = [msg, sig.r.x, sig.r.y]
    field e = poseidon(3, inputs)
    // assert s_bits and e_bits add up to s and e
    field s_assemb = 0
    field e_assemb = 0
    for u32 i in 0..WIDTH do
        s_assemb = s_assemb * 2
        e_assemb = e_assemb * 2
        if s_bits[i] then
            s_assemb = s_assemb + 1
        endif
        if e_bits[i] then
            e_assemb = e_assemb + 1
        endif
    endfor
    assert(s_assemb == sig.s)
    assert(e_assemb == e)
    Point eq = point_mul(pk.q, e, e_bits)
    Point sp = point_mul(pk.p, sig.s, s_bits)
    Point res = point_add(sig.r, eq)
    assert(res.x == sp.x && res.y == sp.y)
    return 1

def main() -> u32:
    field msg = 6908441180828167112785246881494320159273940089327447106269949444716788494909

    // Generate a pk / sk pair
    Point p = Point {
        x: 2023776050662786605020065854616777351003832247628992350849206310281785027488,
        y: 1079270832837170318396516616249394502719834190979906495690419930531357954746
    }
    field a = 6663691457994429232637728556709855123400985295737706519097842870836531302493
    bool[WIDTH] a_bits = [false, true, true, true, false, true, false, true, true, true, false, true, true, true, false, false, false, false, true, false, false, false, false, false, false, false, true, false, true, false, true, true, false, true, true, false, false, false, false, true, true, true, false, true, false, false, false, false, true, true, false, true, true, true, true, true, true, false, true, false, false, false, false, false, false, true, true, true, true, true, false, false, false, false, true, false, true, true, true, false, true, true, true, true, true, false, false, false, false, false, true, true, false, false, false, true, false, true, true, false, true, false, true, true, true, true, true, true, false, true, false, true, true, true, false, false, false, false, true, true, true, true, false, false, false, false, false, true, true, true, false, false, true, false, true, false, false, true, true, false, false, true, false, false, true, true, true, false, false, false, true, false, false, true, false, true, true, true, false, false, false, false, true, true, false, false, false, false, false, true, false, false, true, false, true, false, false, false, false, true, true, true, false, false, false, true, false, true, true, false, false, true, true, false, true, true, true, true, false, true, false, false, false, false, true, false, true, true, true, true, true, true, false, false, true, false, true, true, true, true, true, true, true, false, true, false, false, false, false, false, true, false, false, false, false, false, false, false, false, false, true, false, false, false, false, false, true, false, true, true, true, false, true]
    Point q = point_mul(p, a, a_bits)

    // Generate a signature
    field k = 3955954614002644556381158561434471324488805491866623303867037346350176036901
    bool[WIDTH] k_bits = [false, true, false, false, false, true, false, true, true, true, true, true, false, true, true, true, true, true, true, false, true, true, false, true, false, true, true, true, true, true, true, false, true, true, true, false, false, false, true, true, false, true, false, false, true, true, true, false, false, false, true, false, true, false, true, false, false, false, true, false, true, false, false, true, false, true, true, true, false, true, true, false, false, false, false, true, false, true, false, false, true, false, false, false, false, false, true, false, false, false, false, true, false, false, false, false, true, false, true, true, true, false, false, false, false, false, true, true, true, true, false, false, false, true, true, false, false, true, false, false, false, true, false, false, false, true, false, false, true, false, false, false, true, false, false, false, true, true, true, false, true, true, true, true, true, false, false, false, true, true, true, false, false, false, false, true, false, false, false, true, true, false, true, false, true, false, false, true, false, true, true, false, true, false, true, false, false, false, false, true, true, true, false, false, true, false, false, false, false, false, true, true, false, true, true, true, true, false, false, false, false, false, false, false, true, true, false, true, false, false, true, false, true, false, true, true, true, true, true, true, true, true, false, false, false, false, true, false, true, true, true, true, true, false, false, true, false, false, true, true, false, true, false, false, false, false, false, true, false, false, true, false, true]
    Point r = point_mul(p, k, k_bits)

    field[3] inputs = [msg, r.x, r.y]
    field e = poseidon(3, inputs)
    // e = 664131425432701962192269875939376285542062040011815922607402886695788544729 
    bool[WIDTH] e_bits = [false, false, false, false, true, false, true, true, true, false, true, true, true, true, true, true, false, false, false, true, false, true, false, false, true, false, false, true, true, true, false, true, false, true, true, true, false, false, true, false, false, true, false, false, false, true, true, false, false, false, true, false, false, false, false, true, true, false, false, false, true, false, false, false, false, true, false, false, true, true, true, true, false, true, false, true, false, false, true, true, false, true, false, false, false, true, false, true, false, false, false, false, true, false, true, true, false, false, false, false, true, false, true, false, true, false, false, false, true, true, false, false, true, true, false, true, true, false, true, false, true, true, true, false, true, false, true, false, true, true, true, true, true, true, false, false, false, true, false, true, false, false, true, false, false, false, true, true, false, false, true, false, false, true, false, false, false, true, false, false, false, true, false, false, true, true, true, false, false, false, true, true, true, false, false, false, false, false, true, true, true, false, true, true, false, true, false, true, false, true, false, true, false, false, true, true, false, false, false, false, true, true, false, false, false, false, true, false, true, true, false, true, false, false, true, true, true, false, true, false, true, true, true, true, false, true, true, false, false, true, false, false, true, false, false, false, true, false, false, false, false, false, false, true, false, true, true, false, true, true, false, false, true]

    // s = k + a * e in (mod 7237005577332262213973186563042994240955753618821290553176770668684506720427)
    field s = 4415915127126372096757067153593537022657929051278082364476489088715040314973
    bool[WIDTH] s_bits = [false, true, false, false, true, true, true, false, false, false, false, true, true, false, true, false, true, false, false, false, true, true, true, false, false, false, false, true, true, true, false, false, false, false, false, false, true, false, false, true, false, false, true, true, true, false, true, true, false, true, false, false, true, true, true, true, false, true, true, true, true, true, false, false, true, false, true, true, false, false, true, false, true, true, true, true, true, true, false, false, true, false, false, true, true, true, false, true, false, false, true, false, false, false, false, true, true, true, false, true, true, false, false, true, true, true, true, true, false, false, false, true, false, true, true, true, true, false, true, false, true, true, false, false, true, false, false, false, false, true, false, false, false, true, true, true, true, false, true, true, false, false, false, true, true, false, false, false, true, false, true, true, true, false, true, true, true, false, true, false, false, true, true, false, false, true, false, false, false, false, true, true, false, true, false, true, false, false, false, true, true, false, true, true, false, true, true, false, true, true, true, false, true, true, false, true, false, true, true, false, true, true, true, false, true, true, false, false, true, true, false, false, true, false, true, false, false, false, false, true, false, false, true, false, true, true, true, true, true, false, false, false, true, false, false, false, true, false, true, false, true, false, true, true, false, false, true, false, true, true, true, false, true]

    PublicKey pk = PublicKey {
        p: p,
        q: q
    }
    Signature sig = Signature {
        r: r,
        s: s
    }
    return verify_signature(msg, pk, sig, e_bits, s_bits)